buildscript {
    ext.kotlin_version = '1.3.61'
    repositories{
        mavenCentral()
        jcenter()
    }
    dependencies{
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins{
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

apply plugin: 'kotlin'
apply plugin: 'java-library'

def libVersion = "1.3.1"
group 'com.larryhsiao'
version libVersion

sourceCompatibility = 1.11

repositories {
    mavenCentral()
    maven { url = 'http://172.104.79.181:81/repository/Elizabeth/' }
}

dependencies {
    api "com.silverhetch:clotho:1.15.0"
    testImplementation "org.testfx:testfx-junit5:4.0.16-alpha"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

apply plugin: 'com.jfrog.bintray'
bintray {
    user = 'larryhsiao'
    key = System.getenv('BINTRAY_KEY')
    publications = ['release']
    publish = true
    override = true
    pkg {
        repo = 'Elizabeth'
        name = 'Atropos'
        userOrg = 'larryhsiao'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/LarryHsiao/Atropos.git'
        version {
            name = libVersion
            gpg {
                sign = true
                passphrase = System.getenv('BINTRAY_PASSPHRASE')
            }
        }
    }
}

javafx {
    version = "11"
    modules = ['javafx.web','javafx.controls', 'javafx.fxml', 'javafx.base', 'javafx.swing', 'javafx.media']
}

jar {
    from sourceSets.main.allSource
}

test {
    useJUnitPlatform {
    }
    doFirst {
        jvmArgs += [
                '--add-exports', "javafx.graphics/com.sun.javafx.application=org.testfx",
        ]
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

apply plugin: 'maven-publish'
publishing {
    publications {
        release(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.larryhsiao'
            artifactId 'atropos'
            version libVersion
        }
    }
}